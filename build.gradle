apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'launch4j'
apply plugin: 'eclipse'

gradle.taskGraph.whenReady { taskGraph ->
  if (!taskGraph.hasTask(release)) {
    version = '(develop)'
  }
}

applicationName = 'Li Song Mechlab'
archivesBaseName = 'lsml'
libsDirName = project.rootDir.toString() + '/lib'
mainClassName = 'lisong_mechlab.view.ProgramInit'
sourceCompatibility = 1.7
targetCompatibility = 1.7

buildDir = 'build/gradle'

sourceSets {
  main {
    java {
      srcDir 'src'
    }
    resources {
      srcDir 'resources'
    }
  }
  test{
    java {
      srcDir 'test'
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.thoughtworks.xstream:xstream:1.4.7+', 'org.jfree:jfreechart:1.+', 'net.java.dev.jna:jna:4.0.0+'
  testCompile 'junit:junit:4+', 'org.mockito:mockito-all:1.9.5+', 'pl.pragmatists:JUnitParams:1.0.2+'
}

jar {
  destinationDir = file('build')
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  archiveName="lsml.jar"

  if(version == null){
    version = '0.0.0'
  }

  manifest {
    attributes('Main-Class': mainClassName, 
               'Implementation-Title': 'Li Song Mechlab', 
               'Implementation-Version' : version)
  }
}

launch4j {
  println project.targetCompatibility
  jreMinVersion = "1.7.0"
  mainClassName = project.mainClassName
  dontWrapJar = true
  outputDir = '../'
  icon = project.jar.destinationDir.toString() + '/icon.ico'
  jar = project.jar.archiveName
  manifest = project.jar.destinationDir.toString() + '/lsml.manifest'
}

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'edu.sc.seis.gradle:launch4j:1.0.6+'
  }
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


task wixCandle64(type: Exec) {
  workingDir 'build'
  executable 'candle'
  args '-dg_64bit="yes"', '-dg_version=' + version, '"*.wxs"'
}

task wixLight64(type: Exec) {
  workingDir 'build'
  executable 'light'
  args '-ext', 'WixUIExtension', '"*.wixobj"', '-out', 'LSML_Setup-'+version+'_64bit.msi'
}

wixLight64.dependsOn(wixCandle64);

task wixCandle32(type: Exec) {
  workingDir 'build'
  executable 'candle'
  args '-dg_64bit="no"', '-dg_version=' + version, '"*.wxs"'
}

task wixLight32(type: Exec) {
  workingDir 'build'
  executable 'light'
  args '-ext', 'WixUIExtension', '"*.wixobj"', '-out', 'LSML_Setup-'+version+'_32bit.msi'
}

wixLight32.dependsOn(wixCandle32);
    
task release() {
  println "Building release: " + version
}

release.dependsOn(jar)
release.dependsOn('launch4j')
release.dependsOn(wixLight32)
release.dependsOn(wixLight64)